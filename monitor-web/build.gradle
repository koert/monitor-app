buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
}

plugins {
    id 'maven'
//    id 'java-gradle-plugin'
    id 'java'
    id 'war'
    id 'application'
    id "com.moowork.node" version "0.10"
    id "idea"
}

repositories {
    mavenCentral()
}

// Gradle and NPM/Bower: https://objectpartners.com/2015/04/29/using-gradle-and-bower-to-manage-jscss-dependencies/

dependencies {
    providedCompile libraries.javaxJavaEEApi

    testCompile libraries.junit
    testCompile libraries.mockito
}

node {
    version = '0.12.2'
    npmVersion = '2.7.5'
    download = true
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file("$projectDir/node_modules/bower/bin/bower")
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file("$projectDir/bower.json")
    outputs.files file("$projectDir/bower_components")
    dependsOn npmPackages
}

task bowerSync(type: Sync) {
    from "$projectDir/bower_components"
    into "$projectDir/src/main/webapp/bower_components"
    exclude '**/*.json'
    exclude '**/doc'
//    exclude '**/*.min.js'
//    exclude '**/angular*/index.js'
//    exclude '**/angular*/ng*.js'
//    include 'jquery/dist/jquery.js'
//    include 'angular*/**/*.js'
    dependsOn bowerInstall
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

//sourceCompatibility = 1.8
//targetCompatibility = 1.8

//mainClassName = 'nl.zencode.monitor.server.MonitorGlassfishApp2'

